clear
capture log close
display "This program was last run on $S_DATE at $S_TIME"
#delimit ;

log using "C:\Users\Troy\Box Sync\_ troy's files\research\covid-19\2020-05 - ohio analysis\stata work\covid-19 ohio state all causes by ed analysis - 01 - create sample data - v6 .smcl", replace ;
* read in, revise, & save datasets ;
	* deaths data ;
		* read in data ;
			import excel 
				"C:\Users\Troy\Box Sync\_ troy's files\research\_data\ohio\public health info warehouse\ohio deaths by county, year, & mmwr week (2010-2020) - v3 .xlsx" , 
				sheet("dths- st all ed 25yrs+") firstrow allstring ;
		* drop total row ;
			drop if DeathYearYear == "Total" ;
		* drop data later than week 21 of 2020 ;
			foreach W of numlist 22/53
				{ ;
				drop if DeathYearYear == "2020 **" & MMWRWeekMMWRWeek == "`W'" ;
				} ;
		* drop unneeded vars ;
			drop SORT ;
		* rename vars ;
			rename 
				(DeathEducationLevelEducationLeve DeathYearYear MMWRWeekMMWRWeek Deaths     )
				(ed_attain                        year_cal      week_mmwr        num_deaths ) ;
		* revise 2019 & 2020 year var values ;
			replace year_cal = "2019" if year_cal == "2019 **" ;
			replace year_cal = "2020" if year_cal == "2020 **" ;
		* destring number of deaths var & mmwr week var (to allow sorting);
			destring num_deaths, replace ;
			format num_deaths %9.0fc ;
		* inspect var values ;
			* # deaths ;
				* total ;
					tabstat num_deaths, stat(sum) format(%9.0fc) ;
				* by week ;
					gsort week_mmwr ;
					tabstat num_deaths, stat(sum) format(%9.0fc) by(week_mmwr) ;
					* drop observations w/ unknown mmwr week, where mmwr week is 99, & where mmwr week is blank ;
						drop if week_mmwr == "99" ;
						drop if week_mmwr == "Unknown" ;
						drop if week_mmwr == "" ;
					* destring week var for later use ;
						destring week_mmwr,  replace ;
						tabstat num_deaths, stat(sum) format(%9.0fc) by(week_mmwr) ;
				* by calendar year ;
					gsort year_cal ;
					tabstat num_deaths, stat(sum) format(%9.0fc) by(year_cal) ;
				* by age group ;
					tabstat num_deaths, stat(sum) format(%9.0fc) by(ed_attain) ;
					tabstat num_deaths if ed_attain == "UNKNOWN", stat(sum) format(%9.0fc) by(year) ;
					drop if ed_attain == "UNKNOWN" ;
					tabstat num_deaths, stat(sum) format(%9.0fc) ;
		* revise ed attainment values & collapse ;
			rename ed_attain ed_attain_orig ;
			gen     ed_attain = "" ;
			replace ed_attain = "no_hs_dipl"     if ed_attain_orig == "8TH GRADE OR LESS" | ed_attain_orig == "9TH THRU 12TH GRADE; NO DIPLOMA" ; 
			replace ed_attain = "hs_dipl_ged"    if ed_attain_orig == "HIGH SCHOOL GRADUATE OR GED" ; 
			replace ed_attain = "coll_wo_bach" if ed_attain_orig == "ASSOCIATE DEGREE (E.G. AA AS)" | ed_attain_orig == "COLLEGE BUT NO DEGREE" ; 
			replace ed_attain = "coll_w_bach"  if ed_attain_orig == "BACHELORS DEGREE (E.G. BA AB BS)" | ed_attain_orig == "MASTERS DEGREE (E.G. MA MS..)" | ed_attain_orig == "DOCTORATE DEGREE OR PROFESSIONAL DEGREE" ; 
			drop ed_attain_orig ;
			collapse (sum) num_deaths, by(year_cal week_mmwr ed_attain) ;
		* save data ;
			gsort year_cal week_mmwr ed_attain ;
			tempfile deaths ;
			save "`deaths'" ;
			clear ;
	* pop data ;
		* read in data ;
			import excel 
				"C:\Users\Troy\Box Sync\_ troy's files\research\_data\ohio\public health info warehouse\ohio deaths by county, year, & mmwr week (2010-2020) - v3 .xlsx" , 
				sheet("pop - st & ed") firstrow allstring ;
		* drop total rows ;
			drop if EdAttain == "Total" ;
		* rename vars ;
			rename 
				(Year     Count     EdAttain )
				(year_cal pop_total ed_attain_orig ) ;
		* destring pop var ;
			destring pop_total, replace ;
			format pop_total %9.0fc ;
		* revise ed attainment values ;
			gen     ed_attain = "" ;
			replace ed_attain = "no_hs_dipl"     if ed_attain_orig == "No high school diploma" ; 
			replace ed_attain = "hs_dipl_ged"    if ed_attain_orig == "High school or equivalent" ; 
			replace ed_attain = "coll_wo_bach" if ed_attain_orig == "Some college, less than 4-yr degree" ; 
			replace ed_attain = "coll_w_bach"  if ed_attain_orig == "Bachelor's degree or higher" ; 
			drop ed_attain_orig ;
		* approximate 2019 & 2020 pop by using 2013-2018 compound annual growth rate ;
			* create 2019 & 2020 observations for each level of ed attainment ;
				expand 3 if year_cal == "2018" ;
				gsort ed_attain year_cal ;
				replace year_cal = "2019" if year_cal[_n-1] == "2018" & year_cal == "2018" ;
				replace year_cal = "2020" if year_cal[_n-1] == "2019" & year_cal == "2018" ;
				replace pop_total = . if year == "2019" | year == "2020" ;
			* create vars that equal the 2013 & 2018 values ;
				foreach Y in 2013 2018
					{ ;
					gen pop_total_`Y'_temp = pop_total if year_cal == "`Y'" ;
					by ed_attain: egen pop_total_`Y' = mean(pop_total_`Y'_temp) ;
					} ;
			* calc compound annual growth rate 2013-2018 ;
				gen pop_cagr_2013_2018 = ( pop_total_2018 / pop_total_2013 ) ^ (1/5) - 1 ;
			* replace 2019 & 2020 values ;
				replace pop_total = ( 1 + pop_cagr_2013_2018 )   * pop_total_2018 if year == "2019" ;
				replace pop_total = ( 1 + pop_cagr_2013_2018 )^2 * pop_total_2018 if year == "2020" ;
			* drop unneeded vars ;
				drop pop_cagr_2013_2018 pop_total_* ;
		* scale pops to ones (currently in thousands) ;
			replace pop_total = 1000 * pop_total ;
		* inspect var values ;
			* pop ;
				* total ;
					tabstat pop_total, stat(sum) format(%12.0fc) ;
				* by calendar year ;
					tabstat pop_total, stat(sum) format(%12.0fc) by(year_cal) ;
				* by age group ;
					tabstat pop_total, stat(sum) format(%9.0fc) by(ed_attain) ;
		* save data ;
			gsort year_cal ed_attain ;
			tempfile pop ;
			save "`pop'" ;
			clear ;
* merge datasets ;
	* open deaths dataset ;
		use "`deaths'", clear ;
	* merge pop dataset ;
		merge m:1 year_cal ed_attain using "`pop'" ;
		drop _merge ;
* revise & create vars ;
	* epi year var & corresponding dummy vars (will use 2010 as reference year) ;
		* dummy vars ;
			gen     dum_year_epi_2009 = 0 ;
			replace dum_year_epi_2009 = 1 if year_cal == "2010" & week_mmwr <= 26 ;
			gen     dum_year_epi_2011 = 0 ;
			replace dum_year_epi_2011 = 1 if year_cal == "2011" & week_mmwr >= 27 ;
			replace dum_year_epi_2011 = 1 if year_cal == "2012" & week_mmwr <= 26 ;
			gen     dum_year_epi_2012 = 0 ;
			replace dum_year_epi_2012 = 1 if year_cal == "2012" & week_mmwr >= 27 ;
			replace dum_year_epi_2012 = 1 if year_cal == "2013" & week_mmwr <= 26 ;
			gen     dum_year_epi_2013 = 0 ;
			replace dum_year_epi_2013 = 1 if year_cal == "2013" & week_mmwr >= 27 ;
			replace dum_year_epi_2013 = 1 if year_cal == "2014" & week_mmwr <= 26 ;
			gen     dum_year_epi_2014 = 0 ;
			replace dum_year_epi_2014 = 1 if year_cal == "2014" & week_mmwr >= 27 ;
			replace dum_year_epi_2014 = 1 if year_cal == "2015" & week_mmwr <= 26 ;
			gen     dum_year_epi_2015 = 0 ;
			replace dum_year_epi_2015 = 1 if year_cal == "2015" & week_mmwr >= 27 ;
			replace dum_year_epi_2015 = 1 if year_cal == "2016" & week_mmwr <= 26 ;
			gen     dum_year_epi_2016 = 0 ;
			replace dum_year_epi_2016 = 1 if year_cal == "2016" & week_mmwr >= 27 ;
			replace dum_year_epi_2016 = 1 if year_cal == "2017" & week_mmwr <= 26 ;
			gen     dum_year_epi_2017 = 0 ;
			replace dum_year_epi_2017 = 1 if year_cal == "2017" & week_mmwr >= 27 ;
			replace dum_year_epi_2017 = 1 if year_cal == "2018" & week_mmwr <= 26 ;
			gen     dum_year_epi_2018 = 0 ;
			replace dum_year_epi_2018 = 1 if year_cal == "2018" & week_mmwr >= 27 ;
			replace dum_year_epi_2018 = 1 if year_cal == "2019" & week_mmwr <= 26 ;
			gen     dum_year_epi_2019 = 0 ;
			replace dum_year_epi_2019 = 1 if year_cal == "2019" & week_mmwr >= 27 ;
			replace dum_year_epi_2019 = 1 if year_cal == "2020" & week_mmwr <= 26 ;
		* var ;
			gen year_epi = "" ;
			replace year_epi = "2009" if dum_year_epi_2009 == 1 ; 
			replace year_epi = "2010" if year_cal == "2010" & week_mmwr >= 27 ;
			replace year_epi = "2010" if year_cal == "2011" & week_mmwr <= 26 ;
			foreach Y of numlist 11/19
				{ ;
				replace year_epi = "20`Y'" if dum_year_epi_20`Y' == 1 ; 
				} ;
	* mmwr week dummy vars (week 1 is reference week) ;
		foreach W of numlist 2(1)53 
			{ ;
			gen     dum_week_mmwr_`W' = 0 ;
			replace dum_week_mmwr_`W' = 1 if week_mmwr == `W' ;
			} ;
	* fourier terms ;
		gen fourier_theta = 2 * c(pi) * week_mmwr / 52.1775 ;
		gen fourier_sin_theta  = sin(fourier_theta) ;
		gen fourier_cos_theta  = cos(fourier_theta) ;		
		gen fourier_sin_2theta = sin(2 * fourier_theta) ;
		gen fourier_cos_2theta = cos(2 * fourier_theta) ;		
		drop fourier_theta ;
* save complete dataset ;
	gsort year_cal week_mmwr ed_attain ;
	save "C:\Users\Troy\Box Sync\_ troy's files\research\covid-19\2020-05 - ohio analysis\stata work\covid-19 ohio state all causes by ed analysis - sample data - v6 .dta", replace ;
log close ;
end ;
